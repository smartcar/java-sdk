plugins {
    id "com.jfrog.bintray" version "1.8.0"
}

apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.10.0'
    compile 'com.google.code.gson:gson:2.7'
    testCompile sourceSets.main.output
    testCompile 'com.squareup.okhttp3:mockwebserver:3.4.1'
    testCompile 'org.json:json:20160810'
    testCompile 'org.powermock:powermock-release-with-testng-mockito-dependencies:1.6.2'
    testCompile 'org.testng:testng:6.9.12'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compile

    options.header = project.name
    options.windowTitle = "${project.name} - ${project.version}"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'docs'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

test {
    useTestNG()

    // Log the results on every run.
    testLogging {
        outputs.upToDateWhen {false}
        showExceptions = true
        exceptionFormat = 'full'
        showStackTraces = true
    }

    afterTest { desc, result ->
        println "${result.resultType} ${desc.name}"
    }
}

jacoco {
    reportsDir = file("$buildDir/coverage")
}

jacocoTestReport {
    reports {
        html.enabled = true
        xml.enabled = true
    }
}

task setVersion {
    dolast {

    }
}

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true
    publications = ['SmartcarPublication']
    pkg {
        repo = bintrayRepo
        name = libName
        userOrg = bintrayOrg
        licenses = [licenseId]
        desc = libDescription
        websiteUrl = developerWebsite
        issueTrackerUrl = developerIssues
        vcsUrl = vcsRepoUrl
        publicDownloadNumbers = true
//        githubRepo = vcsGithubRepo
        version {
            name = libVersion
            released = new Date()
        }
    }
}

publishing {
    publications {
        SmartcarPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId libGroup
            artifactId libName
            version libVersion
            pom.withXml {
                def root = asNode()

                root.appendNode('name', libName)
                root.appendNode('description', libDescription)
                root.appendNode('url', libUrl)
                root.appendNode('licenses').
                    appendNode('license').
                        appendNode('name', licenseName).parent().
                        appendNode('url', licenseUrl).parent().
                        appendNode('distribution', 'repo')
                root.appendNode('developers').
                    appendNode('developer').
                        appendNode('id', developerId).parent().
                        appendNode('name', developerName).parent().
                        appendNode('email', developerEmail)
                root.appendNode('scm').
                    appendNode('url', vcsRepoUrl).parent()
            }
        }
    }
}
